import java.nio.charset.StandardCharsets

plugins {
	// IDE
	id 'idea'

	// Language
	id 'java'
	id 'java-library'

	// Core frameworks
	// https://plugins.gradle.org/plugin/org.springframework.boot
	id 'org.springframework.boot' version '4.0.2'
	// https://plugins.gradle.org/plugin/io.spring.dependency-management
	id 'io.spring.dependency-management' version '1.1.7'

	// Publishing
	// https://github.com/vanniktech/gradle-maven-publish-plugin/releases
	id 'com.vanniktech.maven.publish' version '0.36.0'

	// Utility
	id 'jacoco'
	// https://plugins.gradle.org/plugin/org.sonarqube
	id 'org.sonarqube' version '7.2.2.6593'
	// https://plugins.gradle.org/plugin/com.diffplug.spotless
	id 'com.diffplug.spotless' version '8.2.1'
	// https://plugins.gradle.org/plugin/me.champeau.jmh
	id 'me.champeau.jmh' version '0.7.3'
}

// Project properties
group = "$GROUP"
version = "$VERSION_NAME"
description = "$POM_DESCRIPTION"

// Source code properties
java {
	def version = JavaVersion.VERSION_17.toString()

	sourceCompatibility = version
	targetCompatibility = version

	toolchain {
		languageVersion = JavaLanguageVersion.of(version)
	}
}

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencyManagement {
	imports {
		// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-dependencies
		mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.1.1'
		// https://mvnrepository.com/artifact/org.testcontainers/testcontainers-bom
		mavenBom 'org.testcontainers:testcontainers-bom:2.0.3'
	}
}

dependencies {
	api 'org.springframework.boot:spring-boot-starter-cache'
	api 'org.springframework.boot:spring-boot-starter-jackson'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-actuator'

	api 'io.micrometer:micrometer-core'

	api 'com.github.ben-manes.caffeine:caffeine'
	api 'io.github.resilience4j:resilience4j-circuitbreaker'

	// For exception handling utilities
	// https://mvnrepository.com/artifact/io.github.suppierk/java-throwable-utils
	implementation 'io.github.suppierk:java-throwable-utils:2.0.2'

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.awaitility:awaitility'
	testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
}

test {
	useJUnitPlatform()
}

// Enable Spotless code formatting rules
spotless {
	java {
		target '**/src/*/java/**/*.java'

		// Aligns with Intellij IDEA default settings
		toggleOffOn('@formatter:off', '@formatter:on')

		googleJavaFormat()
	}

	groovyGradle {
		target '**/*.gradle'

		greclipse()
	}
}

// Swap build tasks to build library only
bootJar {
	enabled = false
}

jar {
	enabled = true
	archiveClassifier.set('')
}

tasks.register('sourceJar', Jar){
	dependsOn classes
	archiveClassifier.set('sources')
	from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar){
	dependsOn javadoc
	archiveClassifier.set('javadoc')
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives javadocJar
	archives sourceJar
}

// Configure several tasks additionally for Gradle
tasks.withType(Copy).configureEach {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = StandardCharsets.UTF_8.name()
	dependsOn(spotlessJavaCheck)
}

test {
	// Report is always generated after tests run
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	// Tests are required to run before generating the report
	dependsOn test

	reports {
		html.required = true
		xml.required = true
		csv.required = false
	}
}

sonar {
	properties {
		property "sonar.projectKey", "SuppieRK_spring-boot-multilevel-cache-starter"
		property "sonar.organization", "suppierk"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.exclusions", "**/cache/example/*.java"
	}
}

jmh {
	jvmArgsAppend = ['-Xmx4G']
	profilers = ['jfr']
	benchmarkMode = ['avgt']
	timeUnit = 'ns'

	warmup = '1s'
	warmupIterations = 2

	timeOnIteration = '1s'
	iterations = 5
}
